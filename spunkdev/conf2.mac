        TITLE   CONF2
        TWOSEG
        RELOC   ^O400000
;
;  CONFORMALITY CHECKING ROUTINE FOR ARRAY ASSIGNMENT STATEMENTS
;
        ENTRY   CONF2
        RADIX   10
;
        OPDEF   ERROR   [^O32B8]
;
R0==0
R1==1
R2==2
R3==3
R4==4
R5==5
R6==6
RXP==15
;
CONF2:
;  .1,2
        HRRZ    R3,.aloc(R1)    ;/ALOC 1
        JUMPE   R3,ERR3
        HRRZ    R3,.aloc(R2)    ;/ALOC 2
        JUMPE   R3,ERR3
;  .3
        HRRZ    R3,.andim(R1)   ;/ANDIM 1
        HRRZ    R4,.andim(R2)   ;/ANDIM 2
        CAME    R3,R4
;  .4
        JRST    ERR1
;  .5
        SETZ    R3,             ;NDX
        ADDI    R1,.bnd1
        ADDI    R2,.bnd1
;  .6
CONF26: MOVE    R5,(R1)
        HLRE    R6,R5
        HRRE    R5,R5
        SUB     R5,R6
        MOVE    R6,(R2)
        HLRE    R0,R6
        HRRE    R6,R6
        SUB     R6,R0
        CAME    R5,R6
;  .7
        JRST    ERR2
;  .8
        ADDI    R1,1
        ADDI    R2,1
        ADDI    R3,1
;  .9
        CAMGE   R3,R4
        JRST    CONF26
;
        POPJ    RXP,
;
;
;  ERROR EXITS
;
ERR1:   ERROR   ME8     ;ARRAYS DO NOT HAVE SAME NUMBER OF DIMENSIONS
ERR2:   ERROR   ME9     ;ARRAYS DO NOT HAVE SAME NUMBER OF ELEMENTS
                        ;IN CORRESPONDING DIMENSIONS
ERR3:   ERROR   ME15    ;REFERENCE TO UNDECLARED ARRAY
;
;
        LIT
        END
